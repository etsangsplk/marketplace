/**
 * @author Nikita Kolytschew
 * @version %I%, %G%
 * @since 1.0.0-SNAPSHOT
 */

// default spring-boot build-script
buildscript {
	ext {
		springBootVersion = '1.4.1.RELEASE'
	}
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath "se.transmode.gradle:gradle-docker:1.2"
	}
}

/** apply cpd to the root project, as this was how the plugin is intended to be used
 * using new Gradle plugins DSL see: https://github.com/aaschmid/gradle-cpd-plugin
 **/
plugins {
	id "de.aaschmid.cpd" version "1.0"
	id "com.moowork.node" version "0.13"
}

apply plugin: 'java'
apply plugin: 'spring-boot'

// IDE Plugins
apply plugin: 'eclipse'
apply plugin: 'idea'

// Docker
apply plugin: 'docker'

// custom properties
apply from: "$projectDir/gradle/properties/custom_properties.gradle"

// plugins
apply from: "$projectDir/gradle/plugins/checkstyle_plugin.gradle"
apply from: "$projectDir/gradle/plugins/pmd_plugin.gradle"
apply from: "$projectDir/gradle/plugins/findbugs_plugin.gradle"
apply from: "$projectDir/gradle/plugins/cpd_plugin.gradle"
apply from: "$projectDir/gradle/plugins/node_plugin.gradle"

// default project definition
// docker image prefix will be taken from group
group = "inspectit.rocks"
version = "1.0.1-SNAPSHOT"
description = "Open Source Marketplace"
project.ext.finalName = jar.baseName

// Java Version
sourceCompatibility = 1.8
targetCompatibility = 1.8

// add jar information
jar {
	baseName = 'marketplace'
}


// set spring profile
def activeProfile = project.ext.activeProfile = project.hasProperty('prod') ? prodProfiles : devProfiles
println "\nUsing profile: $project.ext.activeProfile - $activeProfile for $project.name\n"

// tasks
apply from: "$projectDir/gradle/tasks/spring_boot_task.gradle"
apply from: "$projectDir/gradle/tasks/gradle_node_tasks.gradle"
apply from: "$projectDir/gradle/tasks/docker_tasks.gradle"


// define repo to obtain dependencies from
repositories {
	mavenCentral()
	// jcenter()
}

// set dependencies
dependencies {
	// default
	compile('org.springframework.boot:spring-boot-starter-actuator',
			'org.springframework.boot:spring-boot-starter-web')

	// test dependencies
	testCompile('org.springframework.boot:spring-boot-starter-test',
			'org.springframework.restdocs:spring-restdocs-mockmvc')

}

// define wrapper version to 3.0
task wrapper(type: Wrapper) {
	description = 'Generates gradlew[.bat] scripts'
	gradleVersion = '3.0'
}

// replace placeholder in application.properties only, since application-dev.properties container hard coded values
processResources {
	filesMatching('**/application.properties') {
		expand(project.properties)
	}
}